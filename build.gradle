plugins {
    id("org.jbake.site").version("5.5.0")
    id("org.ajoberstar.git-publish").version("4.1.1")
    id("org.ajoberstar.grgit").version("5.0.0")
    id("groovy")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.apache.groovy:groovy-all:4.+")
    implementation("org.apache.commons:commons-text:1.10.0")
    testImplementation("org.spockframework:spock-core:2.3-groovy-4.0")
}

jbake {
    clearCache = true
    asciidoctorjVersion = '1.5.8.1'
}

gitPublish {
    repoUri = grgit.remote.list().url.first()
    branch = 'gh-pages'
    contents {
        from(file('build/jbake')) {
            into '.'
        }
    }
}

gitPublishCopy.dependsOn bake
bake.dependsOn 'test'

tasks.register('publish') { dependsOn gitPublishPush }

tasks.register('createPost') {
    doLast {
        def dir = "src/jbake/content/blog/${new Date().format('yyyy')}"
        if (!new File(dir).exists()) {
            new File(dir).mkdirs()
        }
        def path = "${dir}/${filename}.adoc"
        new File(path).text = """\
            = $filename
            Rahul Somasunderam
            ${new Date().format('yyyy-MM-dd')}
            :jbake-type: post
            :jbake-status: published
            :jbake-tags: fitness, tracker, bodybugg, fitbit, fuelband, basis
            :idprefix:
        
            subtitle
        
            body
            """.stripIndent()
        println filename
    }
}

test {
    useJUnitPlatform()
}