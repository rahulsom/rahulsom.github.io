plugins {
    id "com.gradle.build-scan" version "3.10.2"
    id "me.champeau.buildscan-recipes" version "0.2.3"
    id 'org.jbake.site' version '5.5.0'
    id "org.ajoberstar.git-publish" version "3.0.1"
    id "groovy"
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.11'
    implementation 'org.apache.commons:commons-lang3:+'
    testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
}

jbake {
    clearCache = true
    asciidoctorjVersion = '1.5.8.1'
}

gitPublish {
    repoUri = 'https://github.com/rahulsom/rahulsom.github.io.git'
    branch = 'master'
    contents {
        from(file('build/jbake')) {
            into '.'
        }
    }
}

gitPublishCopy.dependsOn bake
bake.dependsOn 'test'

task publish(dependsOn: gitPublishPush, group: 'render',
        description: "Publishes buildDir to GitHub Pages on 'master' branch")

task createPost {
    doLast {
        def dir = "src/jbake/content/blog/${new Date().format('yyyy')}"
        if (!new File(dir).exists()) {
            new File(dir).mkdirs()
        }
        def path = "${dir}/${filename}.adoc"
        new File(path).text = """\
            = $filename
            Rahul Somasunderam
            ${new Date().format('yyyy-MM-dd')}
            :jbake-type: post
            :jbake-status: published
            :jbake-tags: fitness, tracker, bodybugg, fitbit, fuelband, basis
            :idprefix:
        
            subtitle
        
            body
            """.stripIndent()
        println filename
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'

    publishAlways()
}

test {
    useJUnitPlatform()
}